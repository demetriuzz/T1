plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'nu.studer.jooq' version '6.0.1'
    id 'org.liquibase.gradle' version '2.2.0'
}

group = 'ru.demetriuzz'
version = '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

ext {
    // https://docs.spring.io/spring-boot/docs/2.7.3/reference/htmlsingle/#appendix.dependency-versions.properties
    set('postgresqlJdbcVersion', '42.2.14')
    set('jooqVersion', '3.15.1')
    set('testContainersVersion', '1.17.6')
    set('dbUrl', 'jdbc:postgresql://localhost:45432/testdb')
    set('dbUser', 'testdb')
    set('dbPassword', 'testdb')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation "org.postgresql:postgresql:${postgresqlJdbcVersion}"
    implementation 'org.liquibase:liquibase-core'

    liquibaseRuntime('org.liquibase:liquibase-core:4.9.1')
    liquibaseRuntime('javax.xml.bind:jaxb-api:2.3.1')
    liquibaseRuntime('info.picocli:picocli:4.7.0')
    liquibaseRuntime("org.postgresql:postgresql:${postgresqlJdbcVersion}")
    jooqGenerator "org.postgresql:postgresql:${postgresqlJdbcVersion}"

    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.apache.kafka:kafka-clients:2.8.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'

    testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testContainersVersion}"
}

liquibase {
    activities {
        t1_migrate {
            changelogFile "src/main/resources/db/changelog/liquibase-changelog.xml"
            url "${dbUrl}"
            username "${dbUser}"
            password "${dbPassword}"
            logLevel "info"
        }
    }
}

jooq {
    version = "${jooqVersion}"
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        main {
            generateSchemaSourceOnCompilation = false

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.ERROR
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = "${dbUrl}"
                    user = "${dbUser}"
                    password = "${dbPassword}"
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = "t1_schema.*"
                    }
                    generate {
                    }
                    target {
                        packageName = 'ru.demetriuzz.t1.jooq.notes'
                        directory = 'src/generated/jooq/notes'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}
